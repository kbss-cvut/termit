PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX pdp: <http://onto.fel.cvut.cz/ontologies/slovník/agendový/popis-dat/pojem/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/terms/>

CONSTRUCT {
    ?glossary a owl:Ontology ;
              a skos:ConceptScheme ;
              a pdp:glosář ;
              dc:language ?language ;
              dc:title ?label ;
              dc:description ?description ;
              dc:creator ?fullName ;
              dc:created ?created ;
              dc:rights ?rights ;
              owl:imports ?imported ;
              owl:versionIRI ?versionIri ;
              <http://purl.org/vocab/vann/preferredNamespaceUri> ?nsUri ;
              <http://purl.org/vocab/vann/preferredNamespacePrefix> ?nsPrefix ;
              <http://purl.org/ontology/bibo/status> ?status ;
              ?y ?z .
} WHERE {
    GRAPH ?g {
        ?vocabulary pdp:má-glosář ?glossary ;
                    dc:title ?vocabularyLabel .
        ?vocabulary dc:language ?language .
        OPTIONAL {
            ?glossary dc:title ?glossaryLabel .
        }
        OPTIONAL {
            ?vocabulary dc:description ?description .
        }
        OPTIONAL {
            ?glossary owl:versionIRI ?versionIri .
        }
        OPTIONAL {
            ?glossary dc:rights ?rights .
        }
        OPTIONAL {
            ?glossary <http://purl.org/vocab/vann/preferredNamespaceUri> ?nsUri ;
                      <http://purl.org/vocab/vann/preferredNamespacePrefix> ?nsPrefix .
        }
        OPTIONAL {
            ?glossary <http://purl.org/ontology/bibo/status> ?status .
        }
        OPTIONAL {
            ?vocabulary ?y ?z .
            FILTER EXISTS {
                    ?y a <http://onto.fel.cvut.cz/ontologies/application/termit/pojem/vlastní-atribut> .
                }
        }
    }
    OPTIONAL {
        ?vocabulary pdp:importuje-slovník/pdp:má-glosář ?imported .
    }
    OPTIONAL {
        ?createRecord a pdp:vytvoření-entity ;
            pdp:má-změněnou-entitu ?vocabulary ;
            pdp:má-editora ?author ;
            pdp:má-datum-a-čas-modifikace ?created .
        ?author pdp:má-křestní-jméno ?firstName ;
            pdp:má-příjmení ?lastName .
        {
          SELECT (max(?createdDate) AS ?createdDateMax) ?vocabulary
          WHERE {   ?createRecord a pdp:vytvoření-entity ;
                    pdp:má-změněnou-entitu ?vocabulary ;
                    pdp:má-datum-a-čas-modifikace ?createdDate . } GROUP BY ?vocabulary
        }
        FILTER(?createdDateMax = ?created)
    }
    BIND (CONCAT(?firstName, " ", ?lastName) AS ?fullName)
    BIND (COALESCE(?glossaryLabel, ?vocabularyLabel) AS ?label)
}
