##
# This is the default full text search query for repositories without text index.
#
# It searches in term and vocabulary labels and uses a plain string lowercase containment.
##

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dc: <http://purl.org/dc/terms/>

SELECT ?entity ?label ?vocabularyUri ?draft ?type ?snippetField ?snippetText WHERE {
    {
        ?entity a ?term ;
           skos:prefLabel ?label ;
           ?inVocabulary ?vocabularyUri .
        OPTIONAL { ?entity ?isDraft ?draft . }
        BIND (?term as ?type) .
    } UNION {
        ?entity a ?vocabulary ;
           dc:title ?label .
        BIND (?vocabulary as ?type) .
    }
    BIND (?label as ?snippetText) .
    BIND (str("label") as ?snippetField) .
    FILTER CONTAINS(LCASE(?label), LCASE(?searchString)) .
} ORDER BY ?label
