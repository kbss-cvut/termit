##
# Full text search query for GraphDB with a Lucene connector.
##

PREFIX : <http://www.ontotext.com/connectors/lucene#>
PREFIX inst: <http://www.ontotext.com/connectors/lucene/instance#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/terms/>

SELECT DISTINCT ?entity ?label ?vocabularyUri ?draft ?type ?snippetField ?snippetText ?score {
  {
    ?search a inst:label_index .
  }
  UNION
  {
    ?search a inst:defcom_index .
  }
  {
    ?entity rdfs:label ?label .
  } UNION {
    ?entity dc:title ?label .
  }
    ?search :query ?wildCardSearchString ;
          :snippetSize 2000 ;
          :entities ?entity .
  ?entity a ?type ;
          :score ?initScore ;
          :snippets _:s .
  _:s :snippetText ?snippetText ;
          :snippetField ?snippetField .
    OPTIONAL {
        ?entity ?inVocabulary ?vocabularyUri .
    }
    OPTIONAL {
        ?entity ?isDraft ?draft .
    }
    FILTER (?type = ?term || ?type = ?vocabulary)
    FILTER (lang(?label) = ?langTag)
    BIND(IF(lcase(str(?snippetText)) = lcase(str(?splitExactMatch)), ?initScore * 2, IF(CONTAINS(lcase(str(?snippetText)), ?searchString), IF(?snippetField = "label", ?initScore * 1.5, ?initScore), ?initScore)) as ?exactMatchScore)
    BIND(IF(?snippetField = "label", ?exactMatchScore * 2, IF(?snippetField = "definition", ?exactMatchScore * 1.2, ?exactMatchScore)) as ?score)
}
ORDER BY desc(?score)
