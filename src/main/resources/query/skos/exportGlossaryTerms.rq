PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX pdp: <http://onto.fel.cvut.cz/ontologies/slovník/agendový/popis-dat/pojem/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/terms/>

CONSTRUCT {
    ?term a skos:Concept ;
        skos:inScheme ?glossary ;
        skos:prefLabel ?prefLabel ;
        skos:definition ?definition ;
        skos:altLabel ?altLabel ;
        skos:related ?related ;
        dc:source ?source ;
        skos:broader ?parent ;
        skos:topConceptOf ?topOf ;
        skos:broader ?type ;
        skos:broader ?supertype ;
        skos:broader  ?owner ;
        skos:broader ?parentEvent .
} WHERE {
    ?term a skos:Concept ;
        skos:inScheme ?glossary ;
        skos:prefLabel ?prefLabel .
    ?vocabulary pdp:má-glosář ?glossary .
    OPTIONAL {
        ?term skos:definition ?definition .
    }
    OPTIONAL {
        ?term skos:altLabel ?altLabel .
    }
    OPTIONAL {
        ?term skos:related ?related .
    }
    OPTIONAL {
        ?term dc:source ?source .
    }
    OPTIONAL {
        ?term skos:broader ?parent .
        FILTER NOT EXISTS {
            ?term skos:broader ?intermediate .
            ?intermediate skos:broader ?parent .
            FILTER (?parent != ?intermediate)
        }
    }
    OPTIONAL {
    # This ensures only parent-less terms become top concepts of their glossaries
        BIND(IF(EXISTS {?glossary skos:hasTopConcept ?term}, ?glossary, BNODE()) as ?topOf)
        FILTER (!ISBLANK(?topOf))
    }
    OPTIONAL {
        ?term a ?type .
        FILTER (?type != skos:Concept)
        FILTER(!STRSTARTS(STR(?type), "http://www.w3.org/2002/07/owl#"))
        FILTER NOT EXISTS {
            ?term a ?intermediateType .
            ?intermediateType rdfs:subClassOf ?type .
            FILTER (?type != ?intermediateType)
        }
    }
    OPTIONAL {
        ?term rdfs:subClassOf ?supertype .
        FILTER (!sameTerm(?term, ?supertype))
        FILTER(!STRSTARTS(STR(?supertype), "http://www.w3.org/2002/07/owl#"))
        FILTER NOT EXISTS {
            ?term rdfs:subClassOf ?intermediateSupertype .
            ?intermediateSupertype rdfs:subClassOf ?supertype .
            FILTER (!sameTerm(?term, ?supertype) && !sameTerm(?term, ?intermediateSupertype) && !sameTerm(?supertype, ?intermediateSupertype))
        }
    }
    OPTIONAL {
        ?owner <http://onto.fel.cvut.cz/ontologies/ufo/has-part> ?term .
        FILTER NOT EXISTS {
            ?intermediateOwner <http://onto.fel.cvut.cz/ontologies/ufo/has-part> ?term .
            ?owner <http://onto.fel.cvut.cz/ontologies/ufo/has-part> ?intermediateOwner .
            FILTER (?owner != ?intermediateOwner)
        }
    }
    OPTIONAL {
        ?parentEvent <http://onto.fel.cvut.cz/ontologies/ufo/has-participant> ?term .
        FILTER NOT EXISTS {
            ?intermediateParentEvent <http://onto.fel.cvut.cz/ontologies/ufo/has-participant> ?term .
            ?parenEvent <http://onto.fel.cvut.cz/ontologies/ufo/has-participant> ?intermediateParentEvent .
            FILTER (?parentEvent != ?intermediateParentEvent)
        }
    }
}
