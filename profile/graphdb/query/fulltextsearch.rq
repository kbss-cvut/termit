##
# Full text search query for GraphDB with a Lucene connector.
##

PREFIX : <http://www.ontotext.com/connectors/lucene#>
PREFIX inst: <http://www.ontotext.com/connectors/lucene/instance#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT ?entity ?label ?description ?vocabularyUri ?state ?type ?snippetField ?snippetText ?score {
  {
    ?search a inst:label_index .
  }
  UNION
  {
    ?search a inst:defcom_index .
  }
  {
    ?entity skos:prefLabel ?label .
    OPTIONAL {
        ?entity skos:definition ?definition .
    }
    OPTIONAL {
        ?entity skos:scopeNote ?scopeNote .
    }
  } UNION {
    ?entity dc:title ?label .
    OPTIONAL {
        ?entity dc:description ?dcDescription .
    }
  }
    ?search :query ?wildCardSearchString ;
          :snippetSize 250 ;
          :entities ?entity .
  ?entity a ?type ;
          :score ?initScore ;
          :snippets _:s .
  _:s :snippetText ?snippetText ;
          :snippetField ?snippetField .
    OPTIONAL {
        ?entity ?inVocabulary ?vocabularyUri .
    }
    OPTIONAL {
        ?entity ?hasState ?state .
    }
    FILTER (?type = ?term || ?type = ?vocabulary)
    FILTER NOT EXISTS { ?entity a ?snapshot . }
    BIND (?langTagVal as ?langTag) .
    BIND (COALESCE(?entityLang, ?vocabularyLang, ?langTag) AS ?langTag) .
    FILTER (lang(?label) = ?langTag)
    BIND(COALESCE(?definition, COALESCE(?scopeNote, ?dcDescription)) AS ?description)
    FILTER (!BOUND(?description) || lang(?description) = ?langTag)
    BIND(IF(lcase(str(?snippetText)) = lcase(str(?splitExactMatch)), ?initScore * 2, IF(CONTAINS(lcase(str(?snippetText)), ?searchString), IF(?snippetField = "prefLabel", ?initScore * 1.5, ?initScore), ?initScore)) as ?exactMatchScore)
    BIND(IF(?snippetField = "prefLabel", ?exactMatchScore * 2, IF(?snippetField = "definition", ?exactMatchScore * 1.2, ?exactMatchScore)) as ?score)
}
ORDER BY desc(?score)
