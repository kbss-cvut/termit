##
# This is the default full text search query for repositories without text index.
#
# It searches in term and vocabulary labels and uses a plain string lowercase containment.
##

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dc: <http://purl.org/dc/terms/>

SELECT ?entity ?label ?vocabularyUri ?state ?type ?snippetField ?snippetText WHERE {
    {
        ?entity a ?term ;
           skos:prefLabel ?label ;
           ?inVocabulary ?vocabularyUri .
        OPTIONAL { ?entity ?hasState ?state . }
        OPTIONAL {
            ?entity skos:definition ?definition .
        }
        OPTIONAL {
            ?entity skos:scopeNote ?scopeNote .
        }
        BIND (?term as ?type) .
    } UNION {
        ?entity a ?vocabulary ;
           dc:title ?label .
        OPTIONAL {
            ?entity dc:description ?dcDescription .
        }
        BIND (?vocabulary as ?type) .
    }
    BIND (COALESCE(?vocabularyUri, ?entity) as ?vocabularyEntity) .
    OPTIONAL {
        ?vocabularyEntity dc:language ?vocabularyLang .
    }
    OPTIONAL {
        ?entity dc:language ?entityLang .
    }
    BIND (?langTagVal as ?langTag) .
    BIND (COALESCE(?entityLang, ?vocabularyLang, ?langTag) AS ?langTag) .
    BIND (?label as ?snippetText) .
    BIND (str("prefLabel") as ?snippetField) .
    FILTER CONTAINS(LCASE(?label), LCASE(?searchString)) .
    FILTER (lang(?label) = ?langTag)
    FILTER NOT EXISTS { ?entity a ?snapshot . }
    BIND(COALESCE(?definition, COALESCE(?scopeNote, ?dcDescription)) AS ?description)
    FILTER (!BOUND(?description) || lang(?description) = ?langTag)
} ORDER BY ?label
